##基本概念

### 主引导记录（Master Boot Record，MBR）

硬盘的0柱面、0磁头、1扇面称为主引导扇区，FDISK程序写到该扇区的内容称为主引导记录。

####主引导记录由四部分组成：

1. 主引导程序：偏移地址为0000H—0088H，负责从活动分区装载并运行系统引导程序
2. 出错信息数据区：偏移地址0089–00EH为出错信息，00E2H—01BDH全为0。01BDH换成十进制等于16 * 16+16 * 11 + 13 = 256+176+13=446，因此1、2这两部分一共占446字节。
3. 分区表（Disk Partition Table，DPT）：偏移地址01BEH—01FDH，含有四个分区，每个分区16字节，共占64字节。
4. 结束标志字：偏移地址01FEH—01FFH，共两个字节，值分别为0x55和0xAA（若是小端法的系统这个值为0xAA55），这两个值是检验MBR是否有效的标志。

#### 主要作用及工作流程

总的来说MBR的主要作用是检查硬盘分区表是否完好并在其中找到可引导的“活动”分区，最后将活动分区的第一逻辑分区内容装入到内存。

**工作流程**：

- 扫描分区表，找到一个激活（可引导）分区

- 找到激活分区的起始扇区，判断是否是引导扇区，还是根据0x55和0xAA标志来判断

- 将激活分区的引导扇区装载到内存07C00处

- 将控制权交给引导扇区代码（所谓转交控制权就是使用jump指令）

  

## 一个简单例子

```nasm
;主引导程序文件 mbr.S
;-------------------------------------------------------------
SECTION MBR vstart=0x7c00
   mov ax,cs
   mov ds,ax
   mov es,ax
   mov ss,ax
   mov fs,ax 		        ;4-8使用cs寄存器的值取初始化其他寄存器
   mov sp,0x7c00	        ;初始化栈指针

;清屏，利用0x06号功能，上卷全部行即可清屏
;-------------------------------------------------------------
;INT 0x10 功能号：0x06 功能描述：上卷窗口
;-------------------------------------------------------------
;输入：
;AH 功能号
;AL = 上卷的行数（0表示全部）
;BH = 上卷行属性
;(CL, CH) = 窗口的左上角的坐标
;(DL, DH) = 窗口的右下角的坐标
;无返回值：
	mov ax, 0x600
	mov bx, 0x700
	mov cx, 0      			;左上角（0,0）		
	mov dx, 0x184f			;右下角（80,25）
                            ;VGA文本模式中，一行只能容纳80个字符，共25行
                            ;下标从0开始所以0x18=24, 0x4f=79
	int 0x10			    ;int 0x10

;--------------------- 下面这三行代码获取光标位置 -------------------
;.get_cursor 获取当前光标位置，在光标位置打印字符。
    mov ah, 3               ;输入：3号子功能获取光标位置，需要存入ah寄存器
    mov bh, 0               ;bh寄存器存储的是待获取光标的页号

    int 0x10                ;输出：ch=光标开始位行，cl等于光标结束行
                            ;dh=光标所在行，dl=光标所在列号
;------------------------ 获取光标位置结束 -------------------------

;-------------------------- 打印字符串 ----------------------------
	mov ax, message
    mov bp, ax              ;es:bp 为串首地址，es此时同cs一致，
                            ;开头时已经为sreg初始化
    ;光标位置要用到dx寄存器中内容，cx中的光标位置可忽略
    mov cx, 0xc             ;cx为串长度，不包括结束符\0的字符个数
    mov ax, 0x1301          ;子功能13号显示字符及属性，要存入ah
                            ;al设置写字符方式 al=01:显示字符串，光标跟随移动
    mov bx, 0x2             ;bh存储要显示的页号，此处第0页
                            ;bl中是字符属性，属性绿底黑字
	
	int 0x10
;--------------------------  打印字符串结束 -------------------------
    jmp $;使程序悬停在此
	message db "Hello World!"
	times 510-($-$$) db 0
	db 0x55, 0xaa
;-----------------------------------------------------------------
;主引导程序结束

```

### 使用方法：

1. 编写“.S”文件

2. 安装nasm，ubuntu下可直接使用指令安装

3. 使用nasm进程汇编：`nasm -f -ombr.bin mbr.S`，其中-f是设定格式的，默认为bin格式。

4. 使用dd命令将mbr.bin程序加载到之前创建的硬盘hd60M.img中：

   ```shell
   dd if=/xx/mbr.bin of=/xx/hd60M.img bs=512 count=1 conv=notrunc
   ```

   参数说明：

   - if=输入文件路径：指定要读区的文件
   - of=输出文件路径：指定要把指定数据输出到哪里
   - bs=字节数：指定块的大小，dd是以块为单位进行IO操作的。
   - count=块数：指定拷贝的块数
   - seek=块数：把块输出到文件时要跳过多少个块
   - conv=如何转换文件，在追加数据时，最好用notrunc，即不打断文件

   