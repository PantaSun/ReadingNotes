---

title: 第5章 保护模式进阶，向内核迈进
date: 2019-03-25 15:52:25
tags: 
- 操作系统
categories:
- Reading notes
- 自己动手写操作系统
---

## 获取内存容量

### 学习Linux获取内存的方法

在Linux中，使用多种获取内存的方法，若一种失败，则转而使用其他方式。其本质上就是使用BIOS中断0x15，该中断有三个子功能，子功能号要放在寄存器eax和ax中：

- eax=0xe820：遍历主机上全部内存
- ax=0xe801：分别检测低15mb和16mb～4gb的内存，最大支持4gb
- ah=0x88：最多检测出64mb内存，时机内存超过此容量也按照64mb返回。

因为BIOS中断是在实模式下使用的，所以要在进入保护模式之前使用。并且将上述三个方法全部写入到loader.S中，以便于一种方法失效可以使用第二种、第三种方法。如果这三种方法都无法获取内存容量，那只能将机器挂起，停止运行了。

三种方法不管具体如何，其目的都是想获取内存的容量，并将容量值存放在某个约定好的位置，以便于后期使用。

### 利用BIOS中断0x15的子功能0xe820获取内存

使用中断的过程基本一致，大致过程如下：

- 就是在使用int指令之前，在一些特定的寄存器中设置参数，如在eax设置值为0xe820来指定要使用的子功能
- 然后调度用int指令
- 最后从一些特定的寄存器中获取中断指令执行后的结果数据

这三个过程适用于大部分int中断。

从最初的描述中可以看出，0xe820可以遍历机器行的全部内存，所以说该子功能的功能是最全面的，从设置参数的角度来说该子功能需要设置的参数也最多。

0xe820返回的信息较多，包含多个属性字段，其返回值可以使用一个结构体来存放这些数据。这个结构体称为地址范围描述符（Address Range Descriptor Structure，ARDS），其结构具体如下

| 字节偏移量 | 属性名称    | 描述                           |
| ---------- | ----------- | ------------------------------ |
| 0          | BaseAddrLow | 基地址的低32位                 |
| 4          | BaseAddLow  | 基地址的高32位                 |
| 8          | LengthLow   | 内存长度的低32位，以字节为单位 |
| 12         | LengthLow   | 内存长度的高32位，以字节为单位 |
| 16         | Type        | 本段内存的类型                 |

这个数据结构大小为20字节。每次使用int 0x15之后，BIOS就会返回一个这样的结构的数据。

#### 这里有个疑问

为什么要返回内存的基地址和内存长度。内存不就是应该就是内存条的大小么，不就是从0到内存条的最大值么？

首先看下实模式下内存布局：

- 从低地址看：0～0x9FFFF处是DRAM（Dynamic Random Access Memory），即动态随机访问内存。这块空间的大小是640KB，这片地址对应到了DRAM，即插在主板上的内存条。

- 高地址的0xF0000～0xFFFFF，这64KB内存是rom，里面存放的就是BIOS代码

  

  **问题来了，为什么是对应到了DRAM而不是直接访问物理内存？**

- 因为从CPU角度来看，主板上插的内存条并不是它所看到的全部的内存。

- 地址总线宽度决定了可以访问的内存空间的大小，早期16位主机的地址总线为20位，其地址范围是1MB

- 32位主机的地址总线宽度是32位，其地址范围是4GB。

- 在计算机中，不是只有内存条上的内存需要通过地址总线访问，硬盘等其他外设也需要通过地址总线来访问

- 若把全部地址总线都指向内存条，那就没法访问其他外设了

- 因此，地址总线是先给其他外设分配一些地址空间，剩下的地址再指向DRAM

- 所谓地址，就是一串数字，相当于参数，和内存条没有关系

- CPU能访问一个地址，这是地址总线给做的映射，相当于给该地址分配一个存储单元，而该存储单于药么落在某个rom中，要么落在某个外设的内存中，要么落到了物理内存条中。

### 具体代码

```asm
   %include "boot.inc"
   section loader vstart=LOADER_BASE_ADDR
   LOADER_STACK_TOP equ LOADER_BASE_ADDR
   
;构建gdt及其内部的描述符
   GDT_BASE:   dd    0x00000000 
	       dd    0x00000000

   CODE_DESC:  dd    0x0000FFFF 
	       dd    DESC_CODE_HIGH4

   DATA_STACK_DESC:  dd    0x0000FFFF
		     dd    DESC_DATA_HIGH4

   VIDEO_DESC: dd    0x80000007	       ; limit=(0xbffff-0xb8000)/4k=0x7
	       dd    DESC_VIDEO_HIGH4  ; 此时dpl为0

   GDT_SIZE   equ   $ - GDT_BASE
   GDT_LIMIT   equ   GDT_SIZE -	1 
   times 60 dq 0					 ; 此处预留60个描述符的空位(slot)
   SELECTOR_CODE equ (0x0001<<3) + TI_GDT + RPL0         ; 相当于(CODE_DESC - GDT_BASE)/8 + TI_GDT + RPL0
   SELECTOR_DATA equ (0x0002<<3) + TI_GDT + RPL0	 ; 同上
   SELECTOR_VIDEO equ (0x0003<<3) + TI_GDT + RPL0	 ; 同上 

   ; total_mem_bytes用于保存内存容量,以字节为单位,此位置比较好记。
   ; 当前偏移loader.bin文件头0x200字节,loader.bin的加载地址是0x900,
   ; 故total_mem_bytes内存中的地址是0xb00.将来在内核中咱们会引用此地址
   total_mem_bytes dd 0					 
   ;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

   ;以下是定义gdt的指针，前2字节是gdt界限，后4字节是gdt起始地址
   gdt_ptr  dw  GDT_LIMIT 
	    dd  GDT_BASE

   ;人工对齐:total_mem_bytes4字节+gdt_ptr6字节+ards_buf244字节+ards_nr2,共256字节
   ards_buf times 244 db 0
   ards_nr dw 0		      ;用于记录ards结构体数量

   loader_start:
   
;-------  int 15h eax = 0000E820h ,edx = 534D4150h ('SMAP') 获取内存布局  -------

   xor ebx, ebx		      ;第一次调用时，ebx值要为0
   mov edx, 0x534d4150	      ;edx只赋值一次，循环体中不会改变
   mov di, ards_buf	      ;ards结构缓冲区
.e820_mem_get_loop:	      ;循环获取每个ARDS内存范围描述结构
   mov eax, 0x0000e820	      ;执行int 0x15后,eax值变为0x534d4150,所以每次执行int前都要更新为子功能号。
   mov ecx, 20		      ;ARDS地址范围描述符结构大小是20字节
   int 0x15
   jc .e820_failed_so_try_e801   ;若cf位为1则有错误发生，尝试0xe801子功能
   add di, cx		      ;使di增加20字节指向缓冲区中新的ARDS结构位置
   inc word [ards_nr]	      ;记录ARDS数量
   cmp ebx, 0		      ;若ebx为0且cf不为1,这说明ards全部返回，当前已是最后一个
   jnz .e820_mem_get_loop

;在所有ards结构中，找出(base_add_low + length_low)的最大值，即内存的容量。
   mov cx, [ards_nr]	      ;遍历每一个ARDS结构体,循环次数是ARDS的数量
   mov ebx, ards_buf 
   xor edx, edx		      ;edx为最大的内存容量,在此先清0
.find_max_mem_area:	      ;无须判断type是否为1,最大的内存块一定是可被使用
   mov eax, [ebx]	      ;base_add_low
   add eax, [ebx+8]	      ;length_low
   add ebx, 20		      ;指向缓冲区中下一个ARDS结构
   cmp edx, eax		      ;冒泡排序，找出最大,edx寄存器始终是最大的内存容量
   jge .next_ards
   mov edx, eax		      ;edx为总内存大小
.next_ards:
   loop .find_max_mem_area
   jmp .mem_get_ok

;------  int 15h ax = E801h 获取内存大小,最大支持4G  ------
; 返回后, ax cx 值一样,以KB为单位,bx dx值一样,以64KB为单位
; 在ax和cx寄存器中为低16M,在bx和dx寄存器中为16MB到4G。
.e820_failed_so_try_e801:
   mov ax,0xe801
   int 0x15
   jc .e801_failed_so_try88   ;若当前e801方法失败,就尝试0x88方法

;1 先算出低15M的内存,ax和cx中是以KB为单位的内存数量,将其转换为以byte为单位
   mov cx,0x400	     ;cx和ax值一样,cx用做乘数
   mul cx 
   shl edx,16
   and eax,0x0000FFFF
   or edx,eax
   add edx, 0x100000 ;ax只是15MB,故要加1MB
   mov esi,edx	     ;先把低15MB的内存容量存入esi寄存器备份

;2 再将16MB以上的内存转换为byte为单位,寄存器bx和dx中是以64KB为单位的内存数量
   xor eax,eax
   mov ax,bx		
   mov ecx, 0x10000	;0x10000十进制为64KB
   mul ecx		;32位乘法,默认的被乘数是eax,积为64位,高32位存入edx,低32位存入eax.
   add esi,eax		;由于此方法只能测出4G以内的内存,故32位eax足够了,edx肯定为0,只加eax便可
   mov edx,esi		;edx为总内存大小
   jmp .mem_get_ok

;-----------------  int 15h ah = 0x88 获取内存大小,只能获取64M之内  ----------
.e801_failed_so_try88: 
   ;int 15后，ax存入的是以kb为单位的内存容量
   mov  ah, 0x88
   int  0x15
   jc .error_hlt
   and eax,0x0000FFFF
      
   ;16位乘法，被乘数是ax,积为32位.积的高16位在dx中，积的低16位在ax中
   mov cx, 0x400     ;0x400等于1024,将ax中的内存容量换为以byte为单位
   mul cx
   shl edx, 16	     ;把dx移到高16位
   or edx, eax	     ;把积的低16位组合到edx,为32位的积
   add edx,0x100000  ;0x88子功能只会返回1MB以上的内存,故实际内存大小要加上1MB

.mem_get_ok:
   mov [total_mem_bytes], edx	 ;将内存换为byte单位后存入total_mem_bytes处。


;-----------------   准备进入保护模式   -------------------
;1 打开A20
;2 加载gdt
;3 将cr0的pe位置1

   ;-----------------  打开A20  ----------------
   in al,0x92
   or al,0000_0010B
   out 0x92,al

   ;-----------------  加载GDT  ----------------
   lgdt [gdt_ptr]

   ;-----------------  cr0第0位置1  ----------------
   mov eax, cr0
   or eax, 0x00000001
   mov cr0, eax

   jmp dword SELECTOR_CODE:p_mode_start	     ; 刷新流水线，避免分支预测的影响,这种cpu优化策略，最怕jmp跳转，
					     ; 这将导致之前做的预测失效，从而起到了刷新的作用。
.error_hlt:		      ;出错则挂起
   hlt

[bits 32]
p_mode_start:
   mov ax, SELECTOR_DATA
   mov ds, ax
   mov es, ax
   mov ss, ax
   mov esp,LOADER_STACK_TOP
   mov ax, SELECTOR_VIDEO
   mov gs, ax

   mov byte [gs:160], 'P'

   jmp $

```

这里删除了上个版本中第四行的跳转指令，因此需要修改mbr.S中的跳转指令，改为：

jmp LOADER_BASE_ADDR + 0x300

即多了一个0x300

基本上不出意外只会使用第一种方法，具体代码思路就是循环调用int 15h的子功能 0xe820，每次调用结束检查CF和EBX：若在CF位置为0的情况下，EBX返回值为0那么就表示本次返回的是最后一个ards结构。

得到ards后，根据得到的个数进行循环，找到内存最大的容量。使用冒泡法进行寻找最大容量：就是将容量初始值设为0，计算（基址+长度）x 单位大小，与容量变量比较，若大于容量变量，则将容量变量替换为新计算出的值。最后的容量就是最大值。